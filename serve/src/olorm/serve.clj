(ns olorm.serve
  (:require
   [compojure.core :refer [defroutes GET]]
   [hiccup.page :as page]
   [iki.api :as iki]
   [mikrobloggeriet.olorm :as olorm]
   [mikrobloggeriet.jals :as jals]
   [clojure.pprint]
   [org.httpkit.server :as httpkit]))

(defn shared-html-header
  "Shared header content -- for example CSS imports."
  []
  [[:meta {:charset "utf-8"}]
   (hiccup.page/include-css "/vanilla.css")
   (hiccup.page/include-css "/mikrobloggeriet.css")])

(defn feeling-lucky []
  [:a {:href "/random-doc" :class :feeling-lucky} "ðŸŽ²"])

(defn index [_req]
  (let [mikrobloggeriet-announce-url "https://garasjen.slack.com/archives/C05355N5TCL"
        github-olorm-url "https://github.com/iterate/olorm/"
        _tech-forum-url "https://garasjen.slack.com/archives/C2K35RDNJ"
        teodor-url "https://teod.eu/"
        hops-url "https://www.headless-operations.no/"
        iterate-url "https://www.iterate.no/"]
    (page/html5
     (into [:head] (shared-html-header))
     [:body
      [:p (feeling-lucky)]
      [:h1 "Mikrobloggeriet"]
      [:p "Teknologer fra Iterate skriver om teknologi fra hverdagen."]
      [:h2 "OLORM"]
      [:p "OLORM skrives av Oddmund, Lars og Richard."]
      [:p
       (interpose " Â· "
                  (for [olorm (olorm/docs {:repo-path ".."})]
                    [:a {:href (olorm/href olorm)} (:slug olorm)]))]
      [:h2 "JALS"]
      [:p "JALS skrives av JÃ¸rgen, Adrian, Lars og Sindre."]
      [:p
       (interpose " Â· "
                  (for [doc (jals/docs {:repo-path ".."})]
                    [:a {:href (jals/href doc)} (:slug doc)]))]

      [:hr]

      [:section
       [:h2 "Hva er dette for noe?"]
       [:p
        "Mikrobloggeriet er et initiativ der teknologer fra " [:a {:href iterate-url} "Iterate"] " deler ting de bryr seg om i hverdagen. "
        "Vi velger Ã¥ publisere fritt tilgjengelig pÃ¥ Internett fordi vi har tro pÃ¥ Ã¥ dele kunnskap. "
        "Innhold og kode for Mikrobloggeriet pÃ¥ " [:a {:href github-olorm-url} "github.com/iterate/olorm"] ". "
        "Mikrobloggeriet kjÃ¸rer pÃ¥ " [:a {:href hops-url} "Headless Operations"] ". "
        "Hvis du har spÃ¸rsmÃ¥l eller kommentarer, kan du ta kontakt med " [:a {:href teodor-url} "Teodor"] "."]]

      [:section
       [:h2 "Er det mulig Ã¥ diskutere publiserte dokumenter?"]
       [:p "Vi oppfordrer alle til Ã¥ kommentere og diskutere!"
        " Men vi tror det er lettest Ã¥ gjÃ¸re pÃ¥ Slack."
        " Delta gjerne i diskusjonen i trÃ¥d pÃ¥ "
        [:a {:href mikrobloggeriet-announce-url} "#mikrobloggeriet-announce"]
        "!"]]

      [:section
       [:h2 "Jeg er Iterate-teknolog og vil skrive, hva gjÃ¸r jeg?"]
       [:p "Finn deg 2-3 andre Ã¥ skrive med, og snakk med Teodor."
        " Vi setter av en time der vi gÃ¥r gjennom skriveprosessen og installerer tooling."
        " Deretter fÃ¥r dere en \"prÃ¸veuke\" der dere kan prÃ¸ve dere pÃ¥ Ã¥ skrive cirka hver tredje dag."
        " SÃ¥ kan dere bestemme dere for om dere vil fortsette Ã¥ skrive eller ikke."]]
      ])))

(defn olorm-index [_req]
  (page/html5
   (into [:head] (shared-html-header))
   [:body
    [:p (feeling-lucky)]
    [:h1 "Alle OLORM-er"]
    [:table
     [:thead [:td "slug"] [:td "author"] [:td "created"]]
     [:tbody
      (for [olorm (map olorm/load-meta (olorm/docs {:repo-path ".."}))]
        [:tr
         [:td [:a {:href (olorm/href olorm)} (:slug olorm)]]
         [:td (olorm/author-name olorm)]
         [:td (:doc/created olorm)]])]]]))

(def markdown->html
  "Convert markdown to html with pandoc and an in-memory cache"
  (iki/cache-fn-by iki/markdown->html identity))

(defn olorm->html [olorm]
  (when (olorm/exists? olorm)
    (markdown->html (slurp (olorm/index-md-path olorm)))))

(defn olorm [req]
  (tap> req)
  (let [olorm (olorm/->olorm {:slug (:slug (:route-params req))
                              :repo-path ".."})
        {:keys [number]} olorm]
    (tap> (olorm/->olorm olorm))
    {:status (if olorm 200 404)
     :body
     (page/html5
      (into [:head] (shared-html-header))
      [:body
       [:p
        (feeling-lucky)
        " â€” "
        [:a {:href "/"} "mikrobloggeriet"]
        " "
        [:a {:href "/o/"} "o"]
        " â€” "
        [:span (interpose " Â· " (filter some?
                                        [(let [prev (olorm/->olorm {:number (dec number) :repo-path ".."})]
                                           (when (olorm/exists? prev)
                                             [:a {:href (olorm/href prev)} (:slug prev)]))
                                         [:span (:slug olorm)]
                                         (let [prev (olorm/->olorm {:number (inc number) :repo-path ".."})]
                                           (when (olorm/exists? prev)
                                             [:a {:href (olorm/href prev)} (:slug prev)]))]))]]
       (olorm->html olorm)])}))

(defn jals->html [doc]
  (when (jals/exists? doc)
    (markdown->html (slurp (jals/index-md-path doc)))))

(defn jals [req]
  (let [doc (jals/->doc {:slug (:slug (:route-params req))
                         :repo-path ".."})
        {:keys [number]} doc
        html (jals->html doc)]
    {:status (if html 200 404)
     :body
     (page/html5
      (into [:head] (shared-html-header))
      [:body
       [:p (feeling-lucky)
        " â€” "
        [:a {:href "/"} "mikrobloggeriet"]
        " â€” "
        [:span (interpose " Â· " (filter some?
                                        [(let [prev (jals/->doc {:number (dec number) :repo-path ".."})]
                                           (when (jals/exists? prev)
                                             [:a {:href (jals/href prev)} (:slug prev)]))
                                         [:span (:slug doc)]
                                         (let [prev (jals/->doc {:number (inc number) :repo-path ".."})]
                                           (when (jals/exists? prev)
                                             [:a {:href (jals/href prev)} (:slug prev)]))]))]
        ]
       html])}))

(defn random-doc [_req]
  (let [target (or (when-let [random-olorm (olorm/random {:repo-path ".."})]
                     (olorm/href random-olorm))
                   "/")]
    {:status 307 ;; temporary redirect
     :headers {"Location" target}
     :body ""}))

(defroutes app
  (GET "/" req (index req))
  (GET "/health" _req {:status 200 :headers {"Content-Type" "text/plain"} :body "all good!"})
  (GET "/vanilla.css" _req {:status 200 :headers {"Content-Type" "text/css"} :body (slurp "vanilla.css")})
  (GET "/mikrobloggeriet.css" _req {:status 200 :headers {"Content-Type" "text/css"} :body (slurp "mikrobloggeriet.css")})
  (GET "/o/" req (olorm-index req))
  (GET "/o/:slug/" req (olorm req))
  (GET "/j/:slug/" req (jals req))
  (GET "/random-doc" _req random-doc))

(defonce server (atom nil))
(def port 7223)
(defn stop-server [stop-fn] (when stop-fn (stop-fn)) nil)
#_{:clj-kondo/ignore [:clojure-lsp/unused-public-var]}
(defn stop! [] (swap! server stop-server))
#_{:clj-kondo/ignore [:clojure-lsp/unused-public-var]}
(defn start! [_opts]
  (swap! server (fn [old-server]
                  (stop-server old-server)
                  (println (str "olorm.serve running: http://localhost:" port))
                  (httpkit/run-server #'app {:port port}))))
